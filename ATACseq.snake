import pandas

sample_csv = pandas.read_csv('sample_sheet.csv', index_col='name')
CONDITIONS = set(sample_csv['condition'].tolist())
REPS = set(sample_csv['replicate'].tolist())

rule all:
	input:
		expand('results/ATAC{rep}_{condition}_fastqc.html', condition=CONDITIONS, rep=REPS),
		expand('results/ATAC{rep}_{condition}.trimmed.fastq', condition=CONDITIONS, rep=REPS),
		expand('results/GRCh38_index.{ext}', ext=['1.bt2', '2.bt2', '3.bt2', '4.bt2', 'rev.1.bt2', 'rev.2.bt2'])

rule bowtie2_build_gencode:
	input:
		ref = 'GRCh38.primary_assembly.genome.fa.gz'
	output:
		expand('results/GRCh38_index.{ext}', ext=['1.bt2', '2.bt2', '3.bt2', '4.bt2', 'rev.1.bt2', 'rev.2.bt2'])
	params:
		basename = 'results/GRCh38_index'
	threads: 16
	conda:
		'envs/bowtie2_env.yml'
	shell:
		'''
		bowtie2-build --threads {threads} {input.ref} {params.basename}
		'''

rule fastqc:
	input:
		fastq = 'samples/ATAC{rep}_{condition}.fastq.gz'
	output:
		fastqc = 'results/ATAC{rep}_{condition}_fastqc.html'
	params:
		outdir = 'results/'
	threads: 4
	conda:
		'envs/fastqc_env.yml'
	shell:
		'''
		fastqc --threads {threads} {input.fastq} -o {params.outdir}
		'''

rule trimomatic:
	input: 
		fastq = 'samples/ATAC{rep}_{condition}.fastq.gz',
		adapters = 'TruSeq3-SE.fa'
	output:
		trimmed = 'results/ATAC{rep}_{condition}.trimmed.fastq'
	threads: 8
	conda:
		'envs/trimmomatic_env.yml'
	shell:
		'''
		trimmomatic SE -threads {threads} {input.fastq} {output.trimmed} ILLUMINACLIP:{input.adapters}:2:30:10 LEADING:3 TRAILING:3 SLIDINGWINDOW:4:15
		'''
